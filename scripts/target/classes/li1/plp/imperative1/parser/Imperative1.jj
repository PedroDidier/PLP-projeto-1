options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(Imp1Parser)

package li1.plp.imperative1.parser;

import li1.plp.expressions1.util.*;
import li1.plp.expressions2.expression.*;
import li1.plp.imperative1.command.*;
import li1.plp.imperative1.declaration.*;
import li1.plp.imperative1.memory.*;
import li1.plp.imperative1.*;
import java.util.*;

public class Imp1Parser {

    public static void main(String args[]) {
        ListaValor entrada = new ListaValor();
	    Imp1Parser parser;
	    if (args.length == 0) {
	    	System.out.println("Imperativa 1 PLP Parser Version 0.0.2:  Reading from standard input . . .");
	    	parser = new Imp1Parser(System.in);
	    } else  {
	    	System.out.println("Imperativa 1 PLP Parser Version 0.0.2:  Reading from file " + args[0] + " . . .");
	    	try {
	    		parser = new Imp1Parser(new java.io.FileInputStream(args[0]));
	    	} catch (java.io.FileNotFoundException e) {
	        	System.out.println("Java Parser Version 1.0.2:  File " + args[0] + " not found.");
	        	return;
	    	}
	    	
	    	List valores = new LinkedList();
	    	for(int i=1;i<args.length;i++)
	    	{
	    	 	String parametro = args[i];
	    	 	
	    	 	try { 
	    	 		Integer inteiro = Integer.valueOf(parametro);
	    	 		valores.add(new ValorInteiro(inteiro.intValue()));
	    	 		continue;
	    	 	} catch(NumberFormatException e) {
	    	 		
	    	 	}
	    	 	
	    	 	if(parametro.equalsIgnoreCase("true") 
	    	 	    || parametro.equalsIgnoreCase("false")) {
	    	 		Boolean booleano = Boolean.valueOf(parametro);
	    	 		valores.add(new ValorBooleano(booleano.booleanValue()));
	    	 	} else {
	    	 	    valores.add(new ValorString(parametro));
	    	 	}			
	    	}
    	
	    	  entrada = criaListaValor(valores);
	    } 

	    try {
            Programa programa = parser.Input();
            System.out.println("Imperativa 1 PLP Parser Version 0.0.2:  Imperativa2 program parsed successfully.");
            if(programa.checaTipo(new ContextoCompilacaoImperativa(entrada))) {
                ListaValor saida = programa.executar(new ContextoExecucaoImperativa(entrada));
                System.out.println(saida);
            }
            else {
                System.out.println("Erro de tipo");
            }
        } catch (Exception e) {
            System.out.println("Imperativa 1 PLP Parser Version 0.0.2:  Encountered errors during parse.");
            e.printStackTrace();
        }
	}
	
	public static ListaValor criaListaValor(List valores) {
		if(valores.size() == 0) {
			return new ListaValor();
		}
		
		Valor primeiro = (Valor) valores.get(0);
		valores.remove(0);
		return new ListaValor(primeiro, criaListaValor(valores));
		
	}
}

PARSER_END(Imp1Parser)


SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN : /* TOKENS DE EXPRESSOES 1 */
{
  < AND: "and" >
| < OR  : "or" >
| < NOT : "not" >
| < LENGTH      : "length" > 
| < TRUE        : "true" >
| < FALSE       : "false" >
}

TOKEN : /* TOKENS DE IMPERATIVA 1 */
{
  < VAR: "var" >
| < COMAND_SKIP: "skip" >
| < WHILE: "while" >
| < DO: "do" >
| < READ: "read" >
| < WRITE: "write" >
| < IF: "if" >
| < THEN: "then" >
| < ELSE: "else" >
}

TOKEN : /* SEPARATORS */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

TOKEN : /* COMANDOS */
{
  < LOAD: "LOAD" >
| < CLEAN: "CLEAN" >
| < NORMALIZE: "NORMALIZE" >
| < SAVE: "SAVE" >
| < TRANSFORM: "TRANSFORM" >
| < AS: "AS" >
| < DROP: "DROP" >
| < FILL: "FILL" >
| < DROPROWS: "DROPROWS" >
| < RENAME: "RENAME" >
| < REPLACE: "REPLACE" >
| < ADD: "ADD" >
| < MULT: "MULT" >
}

TOKEN : /* OPERATORS */
{
  < ATTRIB: ":=" >
| < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < CONCAT: "++" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOAT_LITERAL:
      (["0"-"9"])+ "." (["0"-"9"])* 
    | "." (["0"-"9"])+ 
    | (["0"-"9"])+
  >
|
  < STRING_LITERAL : "\"" (~["\"", "\n", "\r"])* "\"" >
}


TOKEN : /* STRINGS E IDENTIFICADORES */
{
   < IDENTIFIER: (["a"-"z", "A"-"Z"]) (["a"-"z", "A"-"Z", "0"-"9", "_"])* > // Definindo identificadores
}


Programa Input() :
{ 
    Programa retorno;
}
{
   retorno = PPrograma() <EOF>
   { 
	return retorno; 
   }
}

Programa PPrograma() :
{
	Comando retorno;
}
{
	retorno = PComando()
	{
		return new Programa(retorno);
	}
}

Comando PComando() :
{
	Comando retorno;
}
{
    (
        LOOKAHEAD (PComandoSimples() <SEMICOLON>)
        	retorno = PSequenciaComando()
      	  | retorno = PComandoSimples()
   	)
    {
        return retorno;
    }
}

SequenciaComando PSequenciaComando() :
{
	Comando c1;
	Comando c2;
}
{
	c1 = PComandoSimples()
	<SEMICOLON>
	c2 = PComando()
	{return new SequenciaComando(c1, c2);}
}

IO PIO() :
{
	IO retorno;
}
{
	(
		  retorno = PRead()
		| retorno = PWrite()
	)
	{return retorno;}
}

Read PRead() :
{
	Id id;
}
{
	<READ> <LPAREN>
		id = PId()
		   <RPAREN>
	{return new Read (id);}
}

Write PWrite() :
{
	Expressao exp;
}
{
	<WRITE> <LPAREN>
		exp = PExpressao()
		   <RPAREN>
	{return new Write(exp);}
}

IfThenElse PIfThenElse() :
{
	Expressao expressao;
	Comando comandoThen;
	Comando comandoElse;
}
{
	<IF>
		expressao = PExpressao()
	<THEN>
		comandoThen = PComando()
	<ELSE>
		comandoElse = PComando()
	{return new IfThenElse (expressao, comandoThen, comandoElse);}
}

While PWhile() :
{
	Expressao expressao;
	Comando comando;
}
{
	<WHILE>
		expressao = PExpressao()
	<DO>
		comando = PComando()
	{return new While(expressao, comando);}
}

Skip PSkip() :
{
}
{
	<COMAND_SKIP>
	{return new Skip();}
}

Load PLoad():
{
    Token pathToken;
    Token idTok = null;
    String path;
    String alias;
}
{
    (
        <LOAD> 
        pathToken = <STRING_LITERAL>
        [ <AS> idTok = <IDENTIFIER> ]
        {
            path = pathToken.image.substring(1, pathToken.image.length() - 1); // Remove as aspas
            alias = (idTok != null) ? idTok.image : null;
            return new Load(path, alias);
        }
      | <VAR> idTok = <IDENTIFIER> <LOAD> pathToken = <STRING_LITERAL>
        {
            path = pathToken.image.substring(1, pathToken.image.length() - 1); // Remove as aspas
            return new Load(path, idTok.image);
        }
    )
}

Save PSave() :
{
	Token idTok;
    Token pathToken;
}
{
    <SAVE>
	idTok = <IDENTIFIER>
	<AS>
    pathToken = <STRING_LITERAL>
    {
		String alias = idTok.image;
        String path = pathToken.image.substring(1, pathToken.image.length() - 1);
        return new Save(path, alias);
    }
}

Comando PClean():
{
    Token t1;
    String nomeVariavel;
    String action;
	String coluna;
    List columnList = new ArrayList();
    Map columnValuePairs = new HashMap();
    Map oldNewPairs = new HashMap();
}
{
    <CLEAN>
    t1 = <IDENTIFIER> { nomeVariavel = t1.image; }
    (
        <DROP> 
        { action = "DROP"; }
        columnList = PColumnList()
        { return new Clean(nomeVariavel, action, columnList); }
      | <FILL>
        { action = "FILL"; }
        columnValuePairs = PColumnValuePairs()
        { return new Clean(nomeVariavel, action, columnValuePairs); }
      | <DROPROWS>
        { action = "DROPROWS"; }
        [ columnList = PColumnList() ]
        { return new Clean(nomeVariavel, action, columnList); }
      | <RENAME>
        { action = "RENAME"; }
        oldNewPairs = POldNewPairList()
        { return new Clean(nomeVariavel, action, oldNewPairs, true); }
	  | <REPLACE>
	  	{ action = "REPLACE"; }
		t1 = <IDENTIFIER> { coluna = t1.image; }
		oldNewPairs = PReplacePairList()
        { return new Clean(nomeVariavel, action, coluna, oldNewPairs); }
      | t1 = <IDENTIFIER> 
        { 
            action = t1.image; 
            return new Clean(nomeVariavel, action.toLowerCase());
        }
    )
}

Comando PNormalize():
{
    Token t1;
    String nomeVariavel;
    List columnList = new ArrayList();
}
{
    <NORMALIZE>
    t1 = <IDENTIFIER> { nomeVariavel = t1.image; }
    [ columnList = PColumnList() ]
    {
        if (columnList.isEmpty()) {
            return new Normalize(nomeVariavel);
        } else {
            return new Normalize(nomeVariavel, columnList);
        }
    }
}

Comando PTransform():
{
    Token t1;
    String nomeVariavel;
    String operation;
    Map columnValuePairs = new HashMap();
}
{
    <TRANSFORM>
    t1 = <IDENTIFIER> { nomeVariavel = t1.image; }
    (
        <ADD> 
        { operation = "ADD"; }
        columnValuePairs = PNumericPairs()
        { return new Transform(nomeVariavel, operation, columnValuePairs); }
      | <MULT>
        { operation = "MULT"; }
        columnValuePairs = PNumericPairs()
        { return new Transform(nomeVariavel, operation, columnValuePairs); }
    )
}

Map PNumericPairs():
{
    Token colName, value;
    Map pairs = new HashMap();
    String valorNumerico;
}
{
    colName = <IDENTIFIER> <ASSIGN> (value = <INTEGER_LITERAL> | value = <FLOAT_LITERAL>)
    { 
        valorNumerico = value.image;
        pairs.put(colName.image, valorNumerico); 
    }
    (
        <COMMA> colName = <IDENTIFIER> <ASSIGN> (value = <INTEGER_LITERAL> | value = <FLOAT_LITERAL>)
        {
            valorNumerico = value.image;
            pairs.put(colName.image, valorNumerico); 
        }
    )*
    { return pairs; }
}

Atribuicao PAtribuicao() :
{
	Id id;
	Expressao exp;
}
{
	id  = PId() <ATTRIB> exp = PExpressao()
	{
		return new Atribuicao(id, exp);
	}
}

Id PId() :
{
	Token token;
}
{
	token = <IDENTIFIER>
	{
		return new Id(token.toString());
	}
}


Valor PValorInteiro() :
{
	Token token;
}
{
	token = <INTEGER_LITERAL>
	{
		return new ValorInteiro(Integer.parseInt(token.toString()));
	}
}

Valor PValorBooleano() :
{
}
{
	  <FALSE> { return new ValorBooleano(false); } 
	| <TRUE>  { return new ValorBooleano(true); }
}

Valor PValorString() :
{
	Token token;
}
{
	token = <STRING_LITERAL>
	{
		String tokenStr = token.toString();
		tokenStr = tokenStr.substring(1,tokenStr.length()-1);
		return new ValorString(tokenStr);
	}
}

Valor PValor() :
{
	Valor retorno;
}
{
	(
      	  retorno = PValorInteiro() 
		| retorno = PValorBooleano()
		| retorno = PValorString()
    )
	
	{
		return retorno;
	}
}

Expressao PExpMenos() :
{
	Expressao retorno;
}
{
	<MINUS> retorno = PExpressao()
	{
		return new ExpMenos(retorno);
	}
}

Expressao PExpNot() :
{
	Expressao retorno;
}
{
	<NOT> retorno = PExpressao()
	{
		return new ExpNot(retorno);
	}
}

Expressao PExpLength() :
{
	Expressao retorno;
}
{
	<LENGTH> retorno = PExpressao()
	{
		return new ExpLength(retorno);
	}
}

Expressao PExpPrimaria() :
{
    Expressao retorno;
}
{
	(  
	    retorno = PId()
	  | retorno = PValor() 
	  | <LPAREN> retorno = PExpressao() <RPAREN>
	)
	{
		return retorno; 
	}
}


Expressao PExpUnaria() :
{
	Expressao retorno;
}
{
	(
	  retorno = PExpMenos()
	|
	  retorno = PExpNot() 
	| 
	  retorno = PExpLength()
	)
	{
		return retorno;
	}
}

Expressao PExpBinaria() :
{
	Expressao retorno, param2;
}
{
	(
		  LOOKAHEAD (PExpPrimaria() <CONCAT> )
		  	retorno = PExpConcat()
		| LOOKAHEAD (PExpPrimaria() <MINUS>)
			retorno = PExpSub()
		| LOOKAHEAD (PExpPrimaria() <AND>)
			retorno = PExpAnd()
		| LOOKAHEAD (PExpPrimaria() <OR>)
			retorno = PExpOr()
		| LOOKAHEAD (PExpPrimaria() <EQ>)
			retorno = PExpEquals()
		| LOOKAHEAD (PExpPrimaria() <PLUS>)
			retorno = PExpSoma()
	)
	{
		return retorno;
	}
}

ExpSoma PExpSoma() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<PLUS>
	dir = PExpressao()
	{return new ExpSoma(esq, dir);}
}

ExpSub PExpSub() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<MINUS>
	dir = PExpressao()
	{return new ExpSub(esq, dir);}
}


ExpAnd PExpAnd() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<AND>
	dir = PExpressao()
	{return new ExpAnd(esq, dir);}
}


ExpOr PExpOr() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<OR>
	dir = PExpressao()
	{return new ExpOr(esq, dir);}
}

ExpEquals PExpEquals() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<EQ>
	dir = PExpressao()
	{return new ExpEquals(esq, dir);}
}


ExpConcat PExpConcat() :
{
	Expressao esq;
	Expressao dir;
}
{
	esq = PExpPrimaria()
	<CONCAT>
	dir = PExpressao()
	{return new ExpConcat(esq, dir);}
}

Expressao PExpressao() :
{
	Expressao retorno;
}
{
	(
	    LOOKAHEAD (2) 
	  		retorno = PExpUnaria()
	  | LOOKAHEAD (PExpPrimaria() (<OR> | <AND> | <MINUS> | <PLUS> | <EQ>| <CONCAT>)) 
	  		retorno = PExpBinaria()
	  |
	    retorno = PExpPrimaria()
	)
	{
		return retorno;
	}
}

/*  ADICIONADO OU MODIFICADO DA Imperativa1 PARA A Imperativa2  */

Comando PComandoSimples() :
{
	Comando retorno;
}
{
    (
		retorno = PLoad()
	  | retorno = PSave()
      | retorno = PClean()
      | retorno = PNormalize()
      | retorno = PTransform()
      |	retorno = PSkip()
      | retorno = PAtribuicao()
      | retorno = PComandoDeclaracao()
      | retorno = PWhile()
      | retorno = PIfThenElse()
      | retorno = PIO()
      | <LPAREN> retorno = PComando() <RPAREN>
   	)
    {
        return retorno;
    }
}

ComandoDeclaracao PComandoDeclaracao() :
{
	Declaracao dec;
	Comando comando;
}
{
	<LBRACE> 
		dec = PDeclaracao() <SEMICOLON> comando = PComando() 
	<RBRACE>
 	{
 		return new ComandoDeclaracao(dec, comando);
 	}
}

Declaracao PDeclaracao():
{
	Declaracao retorno;
}
{
	(
		  LOOKAHEAD(PDeclaracaoVariavel() <COMMA>)
          retorno = PDeclaracaoComposta()
		  | retorno = PDeclaracaoVariavel()
		  | <LPAREN> retorno = PDeclaracao() <RPAREN>   
	)
	{return retorno;}
}

DeclaracaoComposta PDeclaracaoComposta():
{
	Declaracao d1;
	Declaracao d2;
}
{
	d1 = PDeclaracaoVariavel() <COMMA> d2 = PDeclaracao()
	{return new DeclaracaoComposta(d1, d2);}
}

DeclaracaoVariavel PDeclaracaoVariavel() :
{
	Id id;
	Expressao exp;
	DeclaracaoVariavel retorno;
}
{
	 <VAR> id = PId() <ASSIGN> exp = PExpressao() 
	   	{retorno = new DeclaracaoVariavel(id, exp);}
	{
		return retorno;
	}
}

List PColumnList():
{
    Token t;
    List columnList = new ArrayList();
}
{
    t = <IDENTIFIER> { columnList.add(t.image); }
    (
        <COMMA> t = <IDENTIFIER> { columnList.add(t.image); }
    )*
    { return columnList; }
}

Map PColumnValuePairs():
{
    Token colName, value;
    Map pairs = new HashMap();
    String valorSemAspas;
}
{
    colName = <IDENTIFIER> <ASSIGN> value = <STRING_LITERAL> 
    { 
        // Remover as aspas do valor da string
        valorSemAspas = value.image.substring(1, value.image.length() - 1);
        pairs.put(colName.image, valorSemAspas); 
    }
    (
        <COMMA> colName = <IDENTIFIER> <ASSIGN> value = <STRING_LITERAL> 
        { 
            // Remover as aspas do valor da string
            valorSemAspas = value.image.substring(1, value.image.length() - 1);
            pairs.put(colName.image, valorSemAspas); 
        }
    )*
    { return pairs; }
}

Map POldNewPairList():
{
    Token oldName, newName;
    Map pairs = new HashMap();
}
{
    oldName = <IDENTIFIER> <ASSIGN> newName = <IDENTIFIER> 
    { pairs.put(oldName.image, newName.image); }
    (
        <COMMA> oldName = <IDENTIFIER> <ASSIGN> newName = <IDENTIFIER> 
        { pairs.put(oldName.image, newName.image); }
    )*
    { return pairs; }
}

Map PReplacePairList():
{
    Map<String, String> map = new HashMap<String, String>();
    Token t1, t2;
}
{
    t1 = <STRING_LITERAL> "=" t2 = <STRING_LITERAL>
    {
        map.put(t1.image.substring(1, t1.image.length() - 1), 
                t2.image.substring(1, t2.image.length() - 1));
    }
    ( "," t1 = <STRING_LITERAL> "=" t2 = <STRING_LITERAL>
        {
            map.put(t1.image.substring(1, t1.image.length() - 1), 
                    t2.image.substring(1, t2.image.length() - 1));
        }
    )*
    { return map; }
}

