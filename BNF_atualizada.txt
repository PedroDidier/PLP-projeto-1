<program> ::= <statement>

<statement> ::= <load_stmt>
              | <clean_stmt>
              | <normalize_stmt>
              | <transform_stmt>
              | <save_stmt>
              | <statement> ";" <statement>

;-------------------------------------------------------
; Carregamento de dados
;-------------------------------------------------------
<load_stmt> ::= "LOAD" <string> ["AS" <identifier>]

;-------------------------------------------------------
; Limpeza de dados
;-------------------------------------------------------
<clean_stmt> ::= "CLEAN" <identifier> <clean_action>

<clean_action> ::= "DROP" <column_list>
                 | "FILL" <column_value_pairs>
                 | "DROPROWS" [ <column_list> ]
                 | "RENAME" <rename_pairs>
                 | "REPLACE" <identifier> <replace_pairs>

;-------------------------------------------------------
; Normalização de dados
;-------------------------------------------------------
<normalize_stmt> ::= "NORMALIZE" <identifier> [ <column_list> ]

;-------------------------------------------------------
; Transformação de dados
;-------------------------------------------------------
<transform_stmt> ::= "TRANSFORM" <identifier> <transform_action>

<transform_action> ::= "ADD" <numeric_pairs>
                     | "MULT" <numeric_pairs>

;-------------------------------------------------------
; Salvamento de dados
;-------------------------------------------------------
<save_stmt> ::= "SAVE" <identifier> "AS" <string>

;-------------------------------------------------------
; Definições auxiliares
;-------------------------------------------------------
<column_list> ::= <identifier>
                | <identifier> "," <column_list>

<column_value_pairs> ::= <column_value_pair>
                       | <column_value_pair> "," <column_value_pairs>

<column_value_pair> ::= <identifier> "=" <string>
                      | <identifier> "=" <number>

<rename_pairs> ::= <rename_pair>
                 | <rename_pair> "," <rename_pairs>

<rename_pair> ::= <identifier> "=" <identifier>

<replace_pairs> ::= <replace_pair>
                  | <replace_pair> "," <replace_pairs>

<replace_pair> ::= <string> "=" <string>

<numeric_pairs> ::= <numeric_pair>
                  | <numeric_pair> "," <numeric_pairs>

<numeric_pair> ::= <identifier> "=" <number>

<value> ::= <number>
          | <string>
          | <identifier>

<number> ::= [0-9]+ ( "." [0-9]+ )?

<identifier> ::= [a-zA-Z_][a-zA-Z0-9_]*

<string> ::= "\"" [^\"]* "\""
           | "'" [^']* "'" 